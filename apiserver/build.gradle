/*
 * This file is part of Bob.
 *
 * Bob is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Bob is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with Bob. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id "java"
    id "application"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "com.github.ben-manes.versions" version "0.36.0"
}

group "org.bob-cd"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven { url "https://clojars.org/repo" }
}

def vertxVersion = "4.0.0.CR2"
def cruxVersion = "20.09-1.12.1-beta"
def prometheusClientVersion = "0.9.0"

dependencies {
    implementation "io.vertx:vertx-core:$vertxVersion"
    implementation "io.vertx:vertx-web:$vertxVersion"
    implementation "io.vertx:vertx-web-client:$vertxVersion"
    implementation "io.vertx:vertx-web-openapi:$vertxVersion"
    implementation "io.vertx:vertx-rabbitmq-client:$vertxVersion"
    implementation "org.slf4j:slf4j-simple:1.6.1"
    implementation "juxt:crux-core:$cruxVersion"
    implementation "juxt:crux-jdbc:$cruxVersion"
    implementation "org.postgresql:postgresql:42.2.18"
    implementation "metosin:jsonista:0.2.7"
    implementation "io.prometheus:simpleclient:$prometheusClientVersion"
    implementation "io.prometheus:simpleclient_vertx:$prometheusClientVersion"

    testImplementation "io.vertx:vertx-junit5:$vertxVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
    testImplementation "org.assertj:assertj-core:3.18.1"
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

compileJava {
    sourceCompatibility = "15"
    targetCompatibility = "15"
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

shadowJar {
    archiveFileName = "apiserver.jar"
}

mainClassName = "Main"
